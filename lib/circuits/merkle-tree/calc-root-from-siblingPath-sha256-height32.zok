// ABOUT

// Function for proving membership of a leaf in a Merkle Tree of height h = 32.
//
//            level h:             root
//                              /       \
//                                 ...
//              ...
//                       /    \    ...   /   \
//            level 1:
//                     /  \  /  \  ... /  \  /  \
//  leaves at level 0:


// IMPORTS

import "hashes/sha256/512bit" as sha256of512
import "./hashes/sha256/padding/shaPad432To512" as shaPad432To512

import "utils/pack/unpack128" as unpack128
import "./packing/unpack1x216To216x1" as unpack1x216To216x1
import "./packing/unpack2x128To256x1" as unpack2x128To256x1

import "./concatenate/orderedConcatenate216x216" as orderedConcatenate216x216


// MAIN

// @param {field[32]} siblingPath - the values of the sibling nodes of the path from the leaf to the root. Assume each field is 216-bits.
// @param {field[2]} leafValue - the value of the leaf. We aim to prove this leaf is in the tree.
// @param {field} leafIndex - the index of the leaf within the leaves (indexing starts from zero). Note: the binary decomposition of a leaf's index gives us the 'left-rightness' of that leaf's path up the Merkle Tree.
// @returns {field} root - the root of the merkle tree

def main(private field[32] siblingPath, private field[2] leafValue, private field leafIndex) -> (field[256]):

  // Hash up the Merkle Tree to the root:

  field[128] leafIndexBits = unpack128(leafIndex)
  field[256] nodeValueBits = unpack2x128To256x1(leafValue)

  for field i in 0..32 do
    field j = 31 - i // iterator for the siblingPath
    field k = 127 - i // iterator for the leafIndexBits

    field[216] siblingNodeValueBits = unpack1x216To216x1(siblingPath[j])

    field[432] preimage432 = orderedConcatenate216x216(leafIndexBits[k], nodeValueBits[40..256], siblingNodeValueBits)

    field[512] preimage512 = shaPad432To512(preimage432)

    nodeValueBits = sha256of512(preimage512[0..256], preimage512[256..512])
  endfor

  field[256] root = nodeValueBits

  return root
